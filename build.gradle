plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

// The current version
def libraryVersion='0.6.5'

group = 'com.github.antkudruk'
version = libraryVersion

java {
    withJavadocJar()
    withSourcesJar()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'net.bytebuddy', name: 'byte-buddy', version: '1.12.18'

    testImplementation 'junit:junit:4.13.2',
            'org.powermock:powermock-core:2.0.9',
            'org.powermock:powermock-module-junit4:2.0.9',
            'org.powermock:powermock-api-mockito2:2.0.9'
    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'
    testCompileOnly 'org.projectlombok:lombok:1.18.24'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.24'
}

if(hasProperty('ossrhUsername') && hasProperty('ossrhPassword')) {
    publishing {
        publications {
            maven(MavenPublication) {
                artifactId = 'uniform-factory'
                from components.java
                versionMapping {
                    usage('java-api') {
                        fromResolutionOf('runtimeClasspath')
                    }
                    usage('java-runtime') {
                        fromResolutionResult()
                    }
                }
                pom {
                    name = 'Uniform Factory'
                    description = 'Builds Adapter based on annotations to handle different classes the common way'
                    url = 'https://github.com/antkudruk'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'antkudruk'
                            name = 'Anton Kudruk'
                            email = 'antkudruk@gmail.com'
                        }
                    }
                    scm {
                        connection = 'scm:svn:https://oss.sonatype.org/content/repositories/snapshots'
                        developerConnection = 'scm:svn:https://oss.sonatype.org/content/repositories/snapshots'
                        url = 'https://oss.sonatype.org/content/repositories/snapshots'
                    }
                }
            }
        }
        repositories {
            maven {
                def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username = ossrhUsername
                    password = ossrhPassword
                }
            }
        }
    }

    signing {
        sign publishing.publications.maven
    }
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

def runExampleFolder(gradlewRelativePath, exampleName, projectType, publishToMavenLocal) {
    def stdout = new ByteArrayOutputStream()
    if (new File("examples/${exampleName}/${projectType}").exists()) {
        stdout.println("   Compiling ${projectType.capitalize()} for the listing ${exampleName}")
        exec {
            workingDir "examples/${exampleName}/plugin"
            def params = [gradlewRelativePath, 'clean', 'build']
            if(publishToMavenLocal) {
                params.add('publishToMavenLocal')
            }
            if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
                params.addAll(0, ['cmd', '/c'])
                commandLine params
            } else {
                commandLine params
            }
            standardOutput = stdout
        }
        println "Output: $stdout"
    }
}

task testExamples {
    doLast {
        def stdout = new ByteArrayOutputStream()
        def files = new File('examples').listFiles()
        def gradlewRelativePath = ['..', '..', '..', 'gradlew'].join(File.separator)
        stdout.println("Test of examples")
        for (File file in files) {
            runExampleFolder(gradlewRelativePath, file.name, 'plugin', true)
            runExampleFolder(gradlewRelativePath, file.name, 'client', false)
            runExampleFolder(gradlewRelativePath, file.name, 'project', false)
        }
    }
    dependsOn(publishToMavenLocal)
}


